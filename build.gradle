plugins {
  id 'idea'
  id 'jacoco'
  id 'checkstyle'
  id 'com.github.spotbugs' version '5.2.1'
  id 'org.sonarqube' version '4.4.1.3373'
  id 'org.checkerframework' version '0.6.35' apply false
}

allprojects {
  group = 'com.codeborne'
  version = '6.18.0-SNAPSHOT'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'checkstyle'
  apply plugin: 'com.github.spotbugs'
  apply plugin: "org.sonarqube"
  apply plugin: 'org.checkerframework'

  checkerFramework {
    checkers = [
      'org.checkerframework.common.util.count.report.ReportChecker'
    ]
    excludeTests = true
    extraJavacArgs = [
      '-AsuppressWarnings=type.anno.before.modifier,type.anno.before.decl.anno',
      '-AassumePure',
      '-AwarnUnneededSuppressions',
      '-AassumeAssertionsAreEnabled',
      '-AReportChecker_warns',
      "-Astubs=${project.projectDir}/reportoptional.astub"
     ]
     excludeTests = true
  }
  ext.checkerFrameworkVersion = '3.41.1-SNAPSHOT'
  dependencies {
    compileOnly "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
    testCompileOnly "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
    checkerFramework "org.checkerframework:checker:${checkerFrameworkVersion}"
  }
  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }
  javadoc.options.addStringOption('Xdoclint:none', '-quiet')
  tasks.withType(Javadoc).all { enabled = false }
}

apply from: 'gradle/compilation.gradle'
apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/code-analysis.gradle'
apply from: 'gradle/coverage.gradle'
apply from: 'gradle/tests.gradle'
apply from: 'gradle/javadoc.gradle'

defaultTasks 'check', 'publishToMavenLocal'

afterEvaluate {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxwarns" << "99999"
    }
}
